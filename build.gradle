plugins {
    id 'java-library'
    id 'eclipse'
    id 'maven-publish'
    id 'checkstyle'
    id 'com.gradleup.shadow' version '8.3.5'
}

version = System.getenv('GITHUB_VERSION') ?: '1.19.4'
group = 'com.onarandombox.multiversesignportals'
description = 'Multiverse-SignPortals'

java.sourceCompatibility = JavaVersion.VERSION_11

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = 'spigot'
        url = uri('https://hub.spigotmc.org/nexus/content/repositories/snapshots/')
    }
    maven {
        name = 'onarandombox'
        url = uri('https://repo.onarandombox.com/content/groups/public/')
    }
}

dependencies {
    implementation('org.bukkit:bukkit:1.13.2-R0.1-SNAPSHOT') {
        exclude group: 'junit', module: 'junit'
    }
    implementation 'com.onarandombox.multiversecore:Multiverse-Core:4.2.2'
    api('com.dumptruckman.minecraft:Logging:1.1.1') {
        exclude group: 'org.bukkit', module: 'bukkit'
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
}

configurations {
    [apiElements, runtimeElements].each {
        it.outgoing.artifacts.removeIf { a -> a.buildDependencies.getDependencies(null).contains(jar) }
        it.outgoing.artifact(shadowJar)
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = 'GitHubPackages'
            url = 'https://maven.pkg.github.com/Multiverse/Multiverse-SignPortals'
            credentials {
                username = System.getenv('GITHUB_ACTOR')
                password = System.getenv('GITHUB_TOKEN')
            }
        }
    }
}

processResources {
    def props = [version: "${project.version}"]
    inputs.properties props
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
    outputs.upToDateWhen { false }
}

checkstyle {
    toolVersion = '6.1.1'
    configFile = file('config/mv_checks.xml')
    ignoreFailures = true
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath
}

shadowJar {
    relocate 'com.dumptruckman.minecraft.util.Logging', 'com.onarandombox.MultiverseSignPortals.util.MVSPLogging'
    relocate 'com.dumptruckman.minecraft.util.DebugLog', 'com.onarandombox.MultiverseSignPortals.util.DebugFileLogger'
    configurations = [project.configurations.compileClasspath]
    archiveFileName = "${project.name}-${project.version}.jar"
}

build.dependsOn shadowJar
jar.enabled = false

